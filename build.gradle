plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'

    // Generate OpenAPI docs as a build step to allow for keeping a static copy of the API docs
    //   in the project repository. The static copy will be updated during the CI/CD pipeline.
    //   See: https://github.com/springdoc/springdoc-openapi-gradle-plugin
    id 'org.springdoc.openapi-gradle-plugin' version '1.9.0'
}

group = project.group
version = project.version

java {
    sourceCompatibility = project.java_version
    targetCompatibility = project.java_version
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot + WebFlux
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-crypto'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    // Spring Data - reactive MongoDB
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

    // SpringDoc OpenAPI doc generator for reactive WebFlux; this will generate and serve Swagger UI
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.8.5'
    implementation 'com.github.therapi:therapi-runtime-javadoc-scribe:0.15.0'
    annotationProcessor 'com.github.therapi:therapi-runtime-javadoc-scribe:0.15.0' // generate from Javadoc

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // JWT & authentication libraries
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1' // for Argon2 password hashing

    // configuration processor
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'org.modelmapper:modelmapper:3.0.0'
}

configurations {
    mockitoAgent
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

    // testing
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:mongodb'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    mockitoAgent('org.mockito:mockito-core') {
        transitive = false
    }
}

tasks {
    test {
        useJUnitPlatform()
        jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
    }
}

springBoot {
    mainClass = 'org.ac.cst8277.chard.matt.litter.App'
}

bootJar {
    archiveFileName = "litter.jar"
}
